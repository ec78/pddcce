/** This gauss code computes Common Correlated Effects (CCE) estimates proposed in:   **/
/** PESARAN, M.H., (2006), ESTIMATION AND INFERENCE IN LARGE HETEROGENEOUS PANELS
WITH A MULTIFACTOR ERROR STRUCTURE, ECONOMETRICA, VOL74, NO.4, PP.967-1012. **/
/** Assume yearly data. If not, year variables should be replaced sequence of numbers **/
/** Please read the CCEgauss6_Readme.pdf file which accompanied with this gauss code  **/

/** Takashi Yamagata 22.08.2008                                                       **/
#include cce.sdf
#include cce_mg.src
#include dcceutil.src

/************************************************************/
/** Specify the directory and name of the output file      **/
/************************************************************/
output file = CCE_out_ec.txt RESET;

/************************************************************/
/** Load cross section id, years, y_it, x_it (first sheet) **/
/************************************************************/
data_file = __FILE_DIR $+ "CCEtestdata.xls";

// Load data
data = loadd(data_file);

// Get variables names
vname = satocv(getHeaders(data_file))';

/* Specify variable lists
** This includes
** yvar            Dependent variable name or index
** xvars           Independent variables that vary with cross-sections
** commonvars      Independent variables that are common across cross-sections
*/
yvar = "y";
xvars = "x1 + x2";
commonvars = "d1";

// Specify cross section information
timevar = "year";
groupvar = "id";


/*****************************************************************/
/** Load cross sectionally common variables d_t (second sheet)  **/
/*****************************************************************/
/** first of all, specify the number of "d" (excluding intercept) **/
nb_d = rows(commonvars);

if nb_d /= 0;
    /* Specify the range of first raw, variables names (SECOND EXCEL SHEET)*/
    dname = xlsreadm(data_file, "b1:b1", 2, "");
    dname = dname[1, 1:nb_d];
    
    /* Specify the range of observed common factors z_t (SECOND EXCEL SHEET)*/
    data_d = xlsreadm(data_file, "b2:b11", 2, "");
endif;

/*****************************************************************/
/** specify the x which would not go for cross section averages **/
/*****************************************************************/
/** Column number in Excel (please put "0" if not applicable) **/
/** eg, no_xbar = {5 6};                                    **/
no_xbar = {0 };

/*****************************************************************/
/** specify the x and id, which has "zeros" due to normalization**/
/*****************************************************************/
/** zero_x: Column number in Excel
zero_id: the id number
eg,  zero_x = 5; zero_id = 3;
(zero_x = 0; zero_id = 0 if not applicable) **/

zero_x = 5;
zero_id = 3;

/******************************************************************/
/** Whether the individual CCE estimation results are reported or not */
/*******************************************************************/
report=1; /*** report=0: not reported, report=1:reported at the end */

/** @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ **/
/** Please do not change below, unless you are familiar with gauss programme **/
/** @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ **/
_dxmiss = miss();
if sumc(sumc(data)).==_dxmiss;
    "Missing Data Somewhere! This code does not allow it.";
    end;
endif;

// Get utility time and id vectors
{ idvec, tvec, t_n, starttvec, endtvec, miny, maxy, mint, maxt } = _getTimeInfo(data, groupvar, timevar);
n = rows(idvec);
ivec = seqa(1, 1, n);

// Cross section averages
data_bar = getCSA(data, groupvar, timevar);
data_bar_l = getCSA(data, groupvar, timevar, 1);

// Get y
y = data[., yvar];
yname = getcolnames(y);


// Choose x 
x = data[., 4:cols(data)];
xname = getcolnames(x)';


// Choose ybar
ybar = data_bar[. , yvar];
ybarname = yname $+ "bar";
ybar = setcolnames(ybar, ybarname);

// Choose z
if nb_d==0;
    data_d = ones(rows(ybar),1);
    dname = "inpt";
else;
    data_d = data_d~ones(rows(ybar),1);
    dname = dname$~"inpt";
endif;
data_d = setcolnames(data_d, dname');

// Choose xbar 
nb_no_xbar = cols(no_xbar);
xbar_temp = data_bar[., 4:cols(data)];

xbar = __delXBars(xbar_temp, no_xbar');
xbarname = getcolnames(xbar) $+ "bar";
xbar = setcolnames(xbar, xbarname);

// Form h = (z, ybar, xbar) */
{ h, hname } = __getHMat(data_d, ybar, xbar, starttvec, miny, endtvec, maxy, maxt);
    
// Mean group estimation
if zero_x==0;
    zero_xx=0;
else;
    zero_xx=zero_x-3;
endif;

if zero_id==0;
    zero_idx=0;
else;
    zero_idx=indnv(zero_id,idvec);
endif;
/* 
** Common parameters
*/
varname_cce = (yname'$~xname$~dname)';
h_in_mg = h[., 1:cols(data_d)];
startvec = starttvec-miny+1;
endtvec = endtvec-maxy+maxt;
years = seqa(miny,maxy,maxt);

struct mgOut mgO;
mgO = _mg(yname', (xname$~dname)', y, x~h_in_mg, n, tvec, startvec, endtvec, years, zero_xx, zero_idx);

struct cdOut cdO;
cdO = cdtest(mgO.e_mg, n, tvec, startvec, endtvec);

struct pcceNWOut pcce_fe;
pcce_fe = pcceNW(varname_cce, y, x, h_in_mg, n, tvec, startvec, endtvec, years, mgO.b_vec[.,1:cols(x)]);

struct mgOut cceO;
cceO = _mg(yname', (xname$~hname)', y, x~h, n, tvec, startvec, endtvec, years, zero_xx, zero_idx);

struct pccenwout pcceO;
pcceO = pcceNW(varname_cce, y, x, h, n, tvec, startvec, endtvec, years, cceO.b_vec[.,1:cols(x)]);

xhname = xname$~hname;
ccei_se={};
ii=1;
do while ii<=cols(xhname);
    if ii == 1;
        xhnamese = xhname[ii]$~"se(NW)";
    else;
        xhnamese=xhnamese$~(xhname[ii]$~"se(NW)");
    endif;
    
    ccei_se=ccei_se~(cceO.b_vec[.,ii]~cceO.se_nw[.,ii]);
    ii=ii+1;
endo;

/** some outputs **/
outwidth 256;
output on;
format /rd 9,5;

"**********************************************************************************************************";
" PESARAN, M.H., (2006), ESTIMATION AND INFERENCE IN LARGE HETEROGENEOUS PANELS WITH A MULTIFACTOR
ERROR STRUCTURE, ECONOMETRICA, VOL74, NO.4, PP.967-1012.";
"**********************************************************************************************************";
/** some outputs **/
"++++++++++++++++ <Preliminary Information on Panel Data> ++ ++++++++++++++++++++++++++++++++++++++++++++";
anames=("Min(year)"~"Max(Year)"~"Min(Ti)"~"Max(Ti)"~"N"~"mean Ti");
$anames|(ftocv(miny~maxy~mint~maxt~n,1,0)~ftocv(meanc(tvec),1,2));
"";
"++++++++++++++++ <OLS Mean Group Estimates> ++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++";
mgO.out_mg;
"";
"   Pesaran's (2004) CD test statistic (residuals):";;
cdO.cd_nt;
"     (Pesaran, M.H., (2004), General Diagnostic Tests for Cross Section Dependence in Panels, CESifo
Working Papers No.1233)"
"";
"   * OLS Mean Group Estimator is simple cross section average of OLS estimator for each cross section unit.";
"   * se(NP) is the standard error based on non-parametric variance estimator of eq(58) in Pesaran (2006).";
"   *  t(.) is associated t-ratio.";
"";
"++++++++++++++++ <Fixed Effects Estimates> +++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++";
pcce_fe.out_pcce;
"";
"   * se(NP) is the standard error based on non-parametric variance estimator of eq(69) in Pesaran (2006).";
"   * se(NW) is the standard error based on Newey-West type variance estimator of eq(74) in Pesaran (2006).";
"   *  t(.) is associated t-ratio.";
"";
"   Cross Section Averages of Estimated Heterogeneous Slops in FE Estimation";
fmat = "%*.*lf";
dname'$~ntos(meanc(pcce_fe.ci_vec), 7);
"";
"++++++++++++++++ <Pesran's (2006) CCE Mean Group Estimates> ++++++++++++++++++++++++++++++++++++++++++++";
cceO.out_mg[1:rows(pcceO.out_pcce),.];
"";
"   * CCE Mean Group Estimator is defined by eq(53).";
"   * se(NP) is the standard error based on non-parametric variance estimator of eq(58) in Pesaran (2006).";
"   *  t(.) is associated t-ratio.";
"";
"   Cross Section Averages of Estimated Heterogeneous Slops in CCE Mean Group Estimation";
cceO.out_mg[rows(pcceO.out_pcce)+1:rows(cceO.out_mg), 1:2];
"";
"++++++++++++++++ <Pesaran's (2006) CCE Pooled Estimates> +++++++++++++++++++++++++++++++++++++++++++++++";
pcceO.out_pcce;
"";
"   * CCE Pooled Estimator is defined by eq(65).";
"   * se(NP) is the standard error based on non-parametric variance estimator of eq(69) in Pesaran (2006).";
"   * se(NW) is the standard error based on Newey-West type variance estimator of eq(74) in Pesaran (2006).";
"   *  t(.) is associated t-ratio.";
"";
"   Cross Section Averages of Estimated Heterogeneous Slopes in CCE Pooled Estimation";
hname'$~ntos(meanc(pcceO.ci_vec), 7);
"";
"--------------------------------------------------------------";
"N used for Cross Section Average (out of";;
$ftocv(n,1,0);;
")";
$ftocv(seqa(miny,1,maxt)~t_n,1,0);

if report==0;
    format /rd 15,7;
    output off;
    end;
elseif report==1;
    "--------------------------------------------------------------";
    "Reports on CCE Estimates of All Cross Section Units";
    "";
    format /rd 9,5;
    a2names = ("ID"$~xhnamese$~"Ti"$~"From"$~"To");
    a2names$|(itos(idvec)$~ntos(real(ccei_se), 3)$~ntos(tvec~starttvec~endtvec, 4));
    "";
    "   * CCE Estimator for a cross section unit is defined by eq(26).";
    "   * se(NW) is the standard error based on Newey-West type variance estimator of eq(50) in Pesaran (2006).";
endif;
output off;
format /rd 15,7;


